
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьРеализацию(Команда)
	
	Объект.СписокДоговоровИСсылокНаРеализацию.Очистить();
	
	СозданиеАктовВозможно = ПроверитьВозможностьПроведенияДлительнойОперации(Объект.Период.ДатаОкончания);
	
	Если СозданиеАктовВозможно Тогда
	
		ДлительнаяОперация = СоздатьАктыНаСервере();
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.Интервал = 1;
		ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		
		ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессе;
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
		ОбновитьОтображениеДанных();
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтрШаблон("          Дата Окончания периода %1 больше текущей даты. 
		|Создание документов реализации за данный период пока невозможно.
		|                        Выберите другой пенриод.", Формат(Объект.Период.ДатаОкончания, "ДЛФ=Д"));
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПроверитьВозможностьПроведенияДлительнойОперации(ОкончаниеПериода)
	СозданиеАктовВозможно = Истина;
	Если ТекущаяДатаСеанса() < ОкончаниеПериода Тогда
		СозданиеАктовВозможно = Ложь;
	КонецЕсли;
	Возврат СозданиеАктовВозможно;
КонецФункции

&НаСервере
Функция СоздатьАктыНаСервере()
	
	НачалоПериода = Объект.Период.ДатаНачала;
	ОкончаниеПериода = Объект.Период.ДатаОкончания;
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ВКМ_МассовоеСозданиеАктов.ВыполнитьДлитедьноеДействие", НачалоПериода, ОкончаниеПериода);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		Сообщение = Новый СообщениеПользователю;
		ВозврашаемыйОтвет = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ВозврашаемыйОтвет.ВсеСозданныеДокументы <> Неопределено Тогда
			Если ВозврашаемыйОтвет.ВсеСозданныеДокументы.Количество() <> 0 Тогда
				ЗагрузитьПолученныеОбъектыВТабличнуюЧасть(ВозврашаемыйОтвет.ВсеСозданныеДокументы);
				Сообщение.Текст = ВозврашаемыйОтвет.ВозвращаемоеСообщение;
				Сообщение.Сообщить();
			Иначе
				НачалоПериода = Объект.Период.ДатаНачала;
				ОкончаниеПериода = Объект.Период.ДатаОкончания;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Реализация за период с %1 по %2 не производилась.", 
					Формат(НачалоПериода, "ДЛФ=Д"), Формат(ОкончаниеПериода, "ДЛФ=Д"));
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПолученныеОбъектыВТабличнуюЧасть(ВсеСозданныеДокументы)
	Объект.СписокДоговоровИСсылокНаРеализацию.Загрузить(ВсеСозданныеДокументы);
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если Прогресс.Прогресс <> Неопределено Тогда
		Состояние("Выполняется длительное действие.", Прогресс.Прогресс.Процент);
	КонецЕсли

КонецПроцедуры

#КонецОбласти